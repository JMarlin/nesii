Name       NESII_MAPPER;

Partno     NESII_MAPPER;

Date       08/28/23;

Revision   01;

Designer   track_zero;

Company    Makaira;

Device     g16v8ma;



/** Inputs **/ 

PIN    1  = CPU_RWB;
PIN    2  = SW_B;
PIN    3  = SW_A;
PIN    4  = VRAM_MIRROR_IN_B;
PIN    5  = VRAM_MIRROR_IN_A; 
PIN    6  = CA12;
PIN    7  = CA13;
PIN    8  = CA14;
PIN    9  = M2;
PIN    11 = ROMSELB;

/*
 Switch Modes:
 00 - RAM A | Peripherals | Vert mirror
 01 - RAM B | Peripherals | Vert mirror
 10 - RAM A | RAM B | Vert mirror
 11 - RAM A | RAM B | Horiz mirror
*/

/** Outputs **/

PIN    12 = PRG_RAM_A13; 
PIN    13 = AII_DEVSELB;
PIN    14 = VRAM_MIRROR_OUT;
PIN    15 = SWITCH_CLK;
PIN    16 = PRG_RAM_A14;
PIN    17 = CPU_WRB;
PIN    18 = PRG_RAM_CEB;
PIN    19 = BIOS_ROM_CEB;

/** Logic Equations **/

PRG_RAM_A13 = CA13;

/* Dev select is triggered at 0xEXXX unless Switch B is on */
AII_DEVSELB =
    (SW_B)
  # (ROMSELB # !CA14 # !CA13 # CA12);

/* If both switches are on, MIRRORING_A is passed through. In all other cases, MIRRORING B is */
VRAM_MIRROR_OUT = 
    (( (SW_B & SW_A)) & VRAM_MIRROR_IN_A)
  # ((!(SW_B & SW_A)) & VRAM_MIRROR_IN_B); 

/* If Switch B is not set, Clock the data bus into Switch A and Switch B on a write to 0xFXXX */ 
SWITCH_CLK = ROMSELB # CPU_RWB # !CA14 # !CA13 # !CA12 # SW_B;
   
/* A14 on the program RAM is 
   - 1 when the switches are 0b01
   - CA14 in all other cases */
PRG_RAM_A14 = 
   ( ((!SW_B) & (SW_A))       )
 # (!((!SW_B) & (SW_A)) & CA14);

CPU_WRB = !CPU_RWB;

/* RAM is selected if we're in the low half of the cart or Switch B is enabled */
PRG_RAM_CEB = 
    ROMSELB    # (!SW_B & CA14); 

/* ROM is selected at reads to 0xFXXX if switch B is off */
BIOS_ROM_CEB = ROMSELB # !CA14 # !CA13 # !CA12 # SW_B # (!CPU_RWB);





