Name       NESII_MAPPER;
Partno     NESII_MAPPER;
Date       08/28/23;
Revision   01;
Designer   track_zero;
Company    Makaira;
Assembly   NESII_MAPPER;
Location   NESII_MAPPER; Device     g16v8ma;


/** Inputs **/ 

PIN    1  = CPU_RWB;
PIN    2  = SW_A;
PIN    3  = SW_B;
PIN    4  = VRAM_MIRROR_IN_B;
PIN    5  = VRAM_MIRROR_IN_A; 
PIN    6  = CA12;
PIN    7  = CA13;
PIN    8  = CA14;
PIN    9  = M2;
PIN    11 = ROMSELB;

/*
 Switch Modes:
 00 - RAM B | Peripherals | ROM | Vert mirror
 10 - RAM A | Peripherals | RAM B | Vert mirror
 01 - RAM A | RAM B | Vert mirror
 11 - RAM A | RAM B | Horiz mirror
*/

/** Outputs **/

PIN    12 = UTILITY_RAM_SELECT; 
PIN    13 = AII_DEVSELB;
PIN    14 = VRAM_MIRROR_OUT;
PIN    15 = SWITCH_CLK;
PIN    16 = PRG_RAM_A14;
PIN    17 = CPU_WRB;
PIN    18 = PRG_RAM_CEB;
PIN    19 = BIOS_ROM_CEB;

/** Logic Equations **/

/* Utility RAM exists at 0x6000-0x7FFF */
UTILITY_RAM_SELECT =
  (!(ROMSELB & M2))/*NESSELB*/ # !CA14 # !CA13;

/* Dev select is triggered at 0xEXXX unless Switch B is on */
AII_DEVSELB =
    (SW_B)
  # (ROMSELB # !CA14 # !CA13 # CA12);

/* If both switches are on, MIRRORING_A is passed through. In all other cases, MIRRORING B is */
VRAM_MIRROR_OUT = 
    (( (SW_B & SW_A)) & VRAM_MIRROR_IN_A)
  # ((!(SW_B & SW_A)) & VRAM_MIRROR_IN_B); 

/* If Switch B is not set, drop the switch clock line on a write to 0xFXXX and latch the data bus on the end of the RWB */ 
SWITCH_CLK = 
  	!( (!CPU_RWB) & (!ROMSELB) & CA14 & CA13 & CA12 & (!SW_B) );

/* A14 on the program RAM is 
   - 1 when the switches are 0b00
   - CA14 in all other cases */
PRG_RAM_A14 = 
   ( ((!SW_B) & (!SW_A))       )
 # (!((!SW_B) & (!SW_A)) & CA14);

CPU_WRB = !CPU_RWB;

/* RAM is selected if:
    - We're in 0x8000-0xDFFF
    - Switch B is on  */
PRG_RAM_CEB = !(
    (!ROMSELB) & (
        (!(CA14 & CA13))
      # (SW_B)
    )
);

/* ROM is selected at 0xFXXX - 0xFFFF if switch B is off and the CPU is not writing (avoid collisions with switch write)*/
BIOS_ROM_CEB = CPU_WRB # ROMSELB # !CA14 # !CA13 # !CA12 # SW_B;





